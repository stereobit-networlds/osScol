/* gps Plugin - DMS2 - JUNE 2014 - by stereobit */

typeof class=S;;

/*xyz obj calibration */
var iIX = -24700;;//-24700;;   // 25700 pour 100%
var iIY = 1;;//1600;;//15500;; /* 120% 1500, 110% 1600 */
var iIZ = 1300;;//3300;;

/* delta computation */
var sIX = "00202.5776";;
var sIY = "610.0";;//"510.0";;
var sIZ =  "4846.0024";;

/*scene global rotation */
var sIAng = "164.3";;

typeof iX = I;;
typeof iY = I;;
typeof iZ = I;;

typeof iOldX = I;;
typeof iOldY = I;;
typeof iOldZ = I;;
var iMaxDHor = 500;;
var iMaxDVer = 500;;
typeof iErrH = I;;
typeof iErrV = I;;

var sFoot   = "30.48";; /*Y delta */
var sMScale = "25000.0";; /*xy delta */
var sWScale = "0.204";; /* x delta */
var sHScale = "0.135";; /* z delta */

/*scene orientation */
var iScale = 110;;     // 100% 
var iAng   = -5900;;   // 5640,5790

typeof lInpData = [S r1];;
var iniPath="locked/conf/stereobit/";;

typeof sno = I;;	
typeof nsno = S;;
typeof sesfile = S;;
var sesfile = "gpsbuf.txt";;

var log=0;;
var verbose=0;;	
var gps_verbose=0;;
var rLog=1;;

typeof timer = Timer;;
typeof timerOn = S;;
var calibrationtime = 20000;; /* 10 sec at start of geo update */
typeof iTimeC = I;;

fun sessionpath() = 
   strcatn iniPath::DMSname::"/"::(nsno)::"/"::nil;;	
   
fun fooCoord (coord) =
	let coord -> [iX iY iZ] in
	strcatn (itoa iX)::","::(itoa iY)::","::(itoa iZ)::nil;;

fun saveLog(fname, s)=
 if (log==1) then
 (
    let strcatn (sessionpath)::fname::nil -> logBuf in
 
	let _appendpack s _getmodifypack logBuf ->append in
	if (append==-1) then
		_createpack s _getmodifypack logBuf
	else nil
 )
 else nil;;

fun echo(x,s)= 
  saveLog "echo.txt" strcat x "\n";
  if (verbose == 1) then
	_DMSevent this "hear" strcat strcat ">> " x "\n" nil
  else nil;;
  
fun _echo(x,s)= 
_DMSevent this "hear" strcat strcat ">> " x "\n" nil;
saveLog "_echo.txt" strcat x "\n";;  

fun gps_echo(x,s)= 
  if (gps_verbose == 1) then
	_DMSevent this "hear" strcat strcat ">> " x "\n" nil
  else nil;;  
  
fun recordLog(fname, s)=
 if (rLog==1) then
 (
    let strcatn (sessionpath)::fname::nil -> logBuf in
 
	let _appendpack s _getmodifypack logBuf ->append in
	if (append==-1) then
		_createpack s _getmodifypack logBuf
	else nil
 )
 else nil;;  

/* return true if physics (gravity and collision)
   is currently activated for local client */
fun C3D_IsPhysicsActivated () = cel.Cel3dphysics;;

/* return true if physics (gravity and collision)
   is currently activated for server */
fun C3D_IsGlobalPhysicsActivated () = cel.Cel3dglobalphysics;;
		
//read data from line array	
fun GetData(line, n) =
  let 1 -> i in
  let 0 -> ind1 in
  let 0 -> ind2 in
  (
    while (i < n) do
    (
      set ind1 = 1 + strfind "," line ind1;
      set i = i + 1;
    );
    set ind1 = 1 + strfind "," line ind1;
    set ind2 = strfind "," line ind1;
    substr line ind1 (ind2 - ind1);
  );;

fun CheckNumChars(str, sta, end) =
  if (sta == end) then
    0 
  else
    let nth_char str sta -> c in
      if (((c >= '0) && (c <= '9)) || (c == '.)) then
        CheckNumChars str (sta + 1) end
      else
        1;;

//read and check data lines		
proto CheckIntegrity = fun [S] [S r1];;
fun CheckIntegrity(line) =

      /* SAVE ROUTE LOG */
      saveLog "backroute.txt" strcat line "\n";

      if (!strcmp (substr line 0 6) "$PGRME") then
      (
        nil;
      )
      else if (!strcmp (substr line 0 6) "$PGRMZ") then
      (
        let GetData line 1 -> data in
          if ((atoi data) != nil) then
            "$PGRMZ"::data::nil
          else
            nil;
      )
      else if (!strcmp (substr line 0 6) "$GPRMC") then
      (
        //_fooS line;
        let GetData line 1 -> dataTim in
        let GetData line 2 -> dataVal in
        let GetData line 3 -> dataLat in
        let GetData line 4 -> dataNor in
        let GetData line 5 -> dataLon in
        let GetData line 6 -> dataEas in
        let GetData line 7 -> dataRun in
        let GetData line 8 -> dataAng in
        let if ((strlen dataTim) == 6) then 0 else 1 -> errTim in
        let if !strcmp dataVal "A" then 0 else 1 -> errVal in
        let if (((strlen dataLat) == 9) && (dataLat != nil) && (strcmp dataLat nil) && (strcmp dataLat "")) then 0 else 1 -> errLat in
        let if !strcmp dataNor "N" then 0 else 1 -> errNor in
        let if (((strlen dataLon) == 10) && (dataLon != nil) && (strcmp dataLon nil) && (strcmp dataLon "")) then 0 else 1 -> errLon in
        let if !strcmp dataEas "E" then 0 else 1 -> errEas in
        // No control for dataRun
        let (errTim + errVal + errLat + errNor + errLon + errEas) -> error in
        let strfind "." dataAng 0 -> ind in
        let strlen dataAng -> len in
          if ((error == 0) && ((atof dataAng) != nil) && (ind == (len - 2)) && (ind != 0) && ((CheckNumChars dataAng 0 len) == 0)) then
            "$GPRMC"::dataAng::nil
          else
            nil;
      )
      else if (!strcmp (substr line 0 6) "$GPGGA") then
      (
        let GetData line 1 -> dataTim in
        let GetData line 2 -> dataLat in
        let GetData line 3 -> dataNor in
        let GetData line 4 -> dataLon in
        let GetData line 5 -> dataEas in
        let if ((strlen dataTim) == 6) then 0 else 1 -> errTim in
        let if (((strlen dataLat) == 9) && (dataLat != nil) && (strcmp dataLat nil) && (strcmp dataLat "")) then 0 else 1 -> errLat in
        let if !strcmp dataNor "N" then 0 else 1 -> errNor in
        let if (((strlen dataLon) == 10) && (dataLon != nil) && (strcmp dataLon nil) && (strcmp dataLon "")) then 0 else 1 -> errLon in
        let if !strcmp dataEas "E" then 0 else 1 -> errEas in
        let (errTim + errLat + errNor + errLon + errEas) -> error in
          if (error ==0) then
            "$GPGGA"::dataLat::dataLon::nil
          else
            nil;
      )
      else if (!strcmp (substr line 0 6) "cbTick") then
      (
        "cbTick"::nil;
      )
      else
        nil;;
		
/*Avatar GPS functions */		
fun SetAvatarPosition(pos) = 
    let pos -> [iX iY iZ] in
	M3setObjVec session M3getFather session cam [iX iY iZ];
0;;

fun SetAvatarAngle(ang) = 
    let ang -> [rX _ _] in
	M3setObjAng session M3getFather session cam [rX 0 0];
0;;

fun GetAvatarPosition() = 
    M3getObjVec session M3getFather session cam;;
  
fun GetAvatarAngle() = 
    M3getObjAng session M3getFather session cam;;
	
/*Camera GPS functions */
fun SetCamPosition(pos) = 
    let pos -> [iX iY iZ] in
	M3setObjVec session cam [iX iY iZ];
0;;

fun SetCamAngle(ang) = 
    let ang -> [rX _ _] in
	M3setObjAng session cam [rX 0 0];
0;;

fun GetCamPosition() = 
    M3getObjVec session cam;;
  
fun GetCamAngle() = 
    M3getObjAng session cam;;	
	
/* avatar position (alternative) ? */
/*let M3getObjVec session M3getFather session (ObGetMain owner) -> [avX avY avZ] in*/
    	
		
//calculate - transfrom lat lot ang to vectors
fun TranslateGeo (param) =  		
	
	set lInpData = lineextr param;
	let hd lInpData -> line in
	/*let nth_list lInpData 1 -> oname in
	let nth_list lInpData 2 -> line_number in

		if (!strcmp line_number "1") then
	    ( _echo strcatn "Line:"::"z"::nil
		0;)
	  else
	    (_echo strcatn "Line:"::"-0-"::nil 
		0;);
	*/
    let CheckIntegrity line -> l in
    (
	  
      if (l == nil) then
      (
	    gps_echo strcat "LINE:" "0"			
        0;
      )
      else if (!strcmp (hd l) "$PGRME") then
      (
	    gps_echo "PGRME:0"
		
	    /* SAVE OBJECT ROUTE IN GPS CMD */
	    let strcatn "PGRME 0,0"::nil -> vline in
        recordLog strcat "cam" "-backroute-gps.txt" strcat vline "\n";			
        0;
      )
      else if (!strcmp (hd l) "$PGRMZ") then
      (
        let (hd tl l) -> data in
        let ftoi (((itof atoi data) -. (atof sIY)) *. (atof sFoot)) -> delta in
        (
          set iY = iIY + delta;
		  /*<<<<<<<<<<<<< get object's current x,z*/
		  let GetAvatarPosition -> [iX Oy iZ] in
          //let GetCamPosition -> [iX Oy iZ] in
		  
          if ((atoi data) == nil) then
          (
            set iErrV = iErrV + 1;
			_echo strcat "Err (data):" (itoa iErrV)
            0;
          )
          else if ((iOldY != nil) && ((abs (iY - iOldY)) > (iMaxDVer * iErrV))) then
		  //else if ((Oy != 0) && ((abs (iY - Oy)) > (iMaxDVer * iErrV))) then
          (
            set iErrV = iErrV + 1;
			_echo strcatn "Err (pos Y):"::(itoa iErrV)::"-["::(itoa Oy)::"-"::(itoa iY)::"]"::"["::(itoa iIY)::"+"::(itoa delta)::"]"::nil
            0;
          )
          else
          (
            set iErrV = 1;
            set iOldY = iY;
			SetAvatarPosition [iX iY iZ];
			//SetCamPosition [iX iY iZ];
			_echo strcatn "Go (pos Y):"::(itoa iErrV)::"-["::(itoa Oy)::"-"::(itoa iY)::"]"::"["::(itoa iIY)::"+"::(itoa delta)::"]"::nil
			gps_echo strcatn "PGRMZ:"::(itoa iX)::","::(itoa iY)::","::(itoa iZ)::nil
			
	        /* SAVE OBJECT ROUTE IN GPS CMD / VECTORS */
	        let strcatn "PGRMZ:"::(data)::nil -> vline in
            recordLog strcat "cam" "-backroute-gps.txt" strcat vline "\n";				
			
			let strcatn "Pos Y,"::(itoa iX)::","::(itoa iY)::","::(itoa iZ)::nil -> out in
			recordLog strcat "cam" "-backroute-vectors.txt" strcat out "\n";
			
            0;
          );
        );
        0;
      )
      else if (!strcmp (hd l) "$GPRMC") then
      (
        let (hd tl l) -> data in
        (
          if ((atof data) == nil) then
            0
          else
          (
		    SetAvatarAngle [(ftoi (-.65536.0 *. ((atof data) /. 360.0))) 0 0];
			//SetCamAngle [(ftoi (-.65536.0 *. ((atof data) /. 360.0))) 0 0];
			let (ftoi (-.65536.0 *. ((atof data) /. 360.0))) -> angle in
			gps_echo strcatn "GPRMC:" :: (itoa angle) :: ",0,0" ::nil
			
	        /* SAVE OBJECT ROUTE IN GPS CMD / VECTORS */
	        let strcatn "GPRMC:"::(data)::nil -> vline in
            recordLog strcat "cam" "-backroute-gps.txt" strcat vline "\n";				
			
			let strcatn "Ang,"::(itoa (ftoi (-.65536.0 *. ((atof data) /. 360.0))))::",0,0"::nil -> out in
			recordLog strcat "cam" "-backroute-vectors.txt" strcat out "\n";
								
            0;
          );
        );
        0;
      )
      else if (!strcmp (hd l) "$GPGGA") then
      (
        let (hd tl l) -> dataH in
        let strlen substr dataH 2 7 -> chkH in
        let atof substr dataH 2 7 -> mntH in
        let atof substr sIZ 2 7 -> mntZ in
        let ftoi ((mntH -. mntZ) *. ((atof sMScale) /. (atof sHScale))) -> deltaH in
        let (hd tl tl l) -> dataW in
        let strlen substr dataW 3 7 -> chkW in
        let atof substr dataW 3 7 -> mntW in
        let atof substr sIX 3 7 -> mntX in
        let ftoi ((mntW -. mntX) *. ((atof sMScale) /. (atof sWScale))) -> deltaW in
        (
          set iX = iIX + deltaW;
          set iZ = iIZ + deltaH;
		  /*<<<<<<<<<<<<< get object's current y*/
		  let GetAvatarPosition -> [Ox iY Oz] in		  
		  //let GetCamPosition -> [Ox iY Oz] in		  

          if ((mntH == nil) || (mntW == nil) || ((chkH + chkW) != 14)) then 
          (
            set iErrH = iErrH + 1;
			_echo strcat "Err (14):" (itoa iErrV)
            0;
          )
          else if (((iOldX != nil) && (iOldZ != nil)) && ((ftoi sqrt (sqr itof (iX - iOldX)) +. (sqr itof (iZ - iOldZ))) > (iMaxDHor * iErrH)))then
		  //else if (((Ox != 0) && (Oz != 0)) && ((ftoi sqrt (sqr itof (iX - Ox)) +. (sqr itof (iZ - Oz))) > (iMaxDHor * iErrH)))then
          (
            set iErrH = iErrH + 1;
			_echo strcatn "Err (pos XZ):"::(itoa iErrH)::"-["::(itoa Ox)::","::(itoa Oz)::"]-["::(itoa iY)::","::(itoa iZ)::"]"::nil
            0;
          )
          else
          (
            set iErrH = 1;
            set iOldX = iX;
            set iOldZ = iZ;
			SetAvatarPosition [iX iY iZ];
			//SetCamPosition [iX iY iZ];
			gps_echo strcatn "GPGGA:"::(itoa iX)::","::(itoa iY)::","::(itoa iZ)::nil
			
	        /* SAVE OBJECT ROUTE IN GPS CMD / VECTORS */
	        let strcatn "GPGGA:"::(dataH)::","::(dataW)::nil -> vline in
            recordLog strcat "cam" "-backroute-gps.txt" strcat vline "\n";				
			
			let strcatn "Pos XZ,"::(itoa iX)::","::(itoa iY)::","::(itoa iZ)::nil -> out in
			recordLog strcat "cam" "-backroute-vectors.txt" strcat out "\n";
							
            0;
          );
        );        
        0;
      )
      else if (!strcmp (hd l) "cbTick") then
      (
	    gps_echo "cbTick:0"
	    /* SAVE OBJECT ROUTE IN GPS CMD */
	    let strcatn "cbTick 0 0"::nil -> vline in
        recordLog strcat "cam" "-backroute-gps.txt" strcat vline "\n";		
        0;
      )
      else
        0;	
  );;
  	
fun CBgethttp(inet,z,s,reason) =
 let z->[content] in 
 if reason==0 then (mutate z <- [strcat content s];nil)
 else if reason==1 then 
   TranslateGeo content
 else nil;;

fun UpdateGeo(param, o)=    
 INETGetURL _channel param 0 @CBgethttp [nil]; 
 echo strcat "get:" param
0;; 	
  
/*get the obj to 0,0,0 position to start and calibrate gps pos */   
fun Init(param, o) =  
  M3setObjVec session M3getFather session cam [0 0 0];
0;;  

/* Load Static Scene **********************************************************/

fun GetSons(h3d) =
  if (h3d == nil) then
    nil
  else
    h3d::(GetSons M3getBrother session h3d);;
		
fun GetObjType(h3d) =  
  if (h3d == nil) then
    nil
  else
	let M3getObjType session h3d -> ret in
    ret;;
	
fun ApplyOrientation(l) =

  if (l == nil) then
    l
  else
    let M3getObjVec session (hd l) -> [x y z] in
    (
	  let M3getObjType session (hd l) -> mC3d_type in
      _echo strcatn "Object type in list:"::(itoa mC3d_type)::nil
  
      let M3objName session (hd l) -> mC3d_name in
      _echo strcatn "Object name in list:"::mC3d_name::nil  	  
	  
	  _echo strcatn "Orientation (x y z):"::(itoa x)::","::(itoa y)::","::(itoa z)::nil
	  
	  //main scene Altitude réelle: 160m, Erreur de 30m prévue
      M3setObjVec session shell [0 15500 0];
      //M3rotateObjExt session shell [iAng 0 0]; /* avatar back->front nav*/
	  
	  //object in list
      M3setObjVec session (hd l) [((x * iScale) / 100) ((y * iScale) / 100) ((z * iScale) / 100)];
	  
	  _echo strcatn "Orientation vector (x y z * scale /100):"::(itoa (x * iScale) / 100)::","::(itoa (y * iScale) / 100)::","::(itoa (z * iScale) / 100)::nil
	  _echo strcatn "Orientation scale:"::(itoa iScale)::nil	  	  
	  
      M3setObjScale session (hd l) iScale;
	  
      ApplyOrientation tl l;
    );;

fun SetOrientation(orientvalue) = 
 if orientvalue then 
 (
  let M3bigFather session cam -> H3d_Obj in
  //let M3getFirstSon session H3d_Obj -> C3d_Obj in
  let M3getFirstSon session shell -> C3d_Obj in  
  
  //get type
  let M3getObjType session C3d_Obj -> mC3d_type in
  _echo strcatn "Object type:"::(itoa mC3d_type)::nil
  //get name
  let M3objName session C3d_Obj -> mC3d_name in
  _echo strcatn "Object name:"::mC3d_name::nil  
  
  let GetSons C3d_Obj -> lH3d in
  ApplyOrientation lH3d;
 )
 else  
 0;;
 
fun SetCamOnTop(camtopatstart) =
 if camtopatstart then
 (
  //M3setObjVec session M3getFather session cam [0 0 0];
  //M3setObjAng session M3getFather session cam [0 0 0];   
  
  /*moves camera on top (leave avatar stand alone)*/
  M3setObjVec session cam [0 0 0];
  M3setObjAng session cam [0 (-16384) 0];  
  M3rotateObjExt session shell [0 0 0];
  M3setObjVec session cam [0 110000 3000];
 )
 else 
 0;;

fun SetCamDefault() =
  //M3setObjVec session M3getFather session cam [0 0 0];
  //M3setObjAng session M3getFather session cam [0 0 0];   
  
  /*moves camera on avatar position*/
  M3setObjVec session cam [0 0 0];
  M3setObjAng session cam [0 0 0];  
  M3rotateObjExt session shell [0 0 0];
  M3setObjVec session cam [0 0 0];
  0;;
    
fun Init_Lights(lightvalue) =
 if lightvalue then
 (
  /* Ambient light */
  let M3createLight session LIGHT_AMBIENT lightvalue nil nil nil nil -> h3dALight in
  M3link session h3dALight shell;
 )
 else 
  0;;    
  
fun SetAvatar(avsetvalue) =  
 if avsetvalue then
 (
  M3setObjVec session M3getFather session cam [iIX iIY iIZ];
	
  set iIX = ((iIX * iScale) / 100);
  set iIY = ((iIY * iScale) / 100);
  set iIZ = ((iIZ * iScale) / 100);
  set iX = iIX;
  set iY = iIY;
  set iZ = iIZ;
  set iOldX = nil;
  set iOldY = nil;
  set iOldZ = nil;
  set iErrH = 1;
  set iErrV = 1;

  M3setObjAng session M3getFather session cam [(ftoi (-.65536.0 *. ((atof sIAng) /. 360.0))) 0 0];
 )
 else 
  0;;	  
  
fun InitializeScene(av, or, li, ca) =
  /* Avatar transformation..disabled */
  /*SetAvatar av;*/

  /*Add scene orientation */
  SetOrientation or;
  
  /*Add ligthing*/
  Init_Lights li;
  
  /*camera on top when start*/
  SetCamOnTop ca;
0;;     

fun rflTimer (t, counter)=
  /*_DMSevent this "top" nil nil;
  let "top" ->  timerOn in*/
  set iTimeC = iTimeC + 1;
  _echo strcatn "Time:"::"onTop-"::(itoa iTimeC+1)::nil
  0;;
  
fun deleteTimer ()=
  _deltimer timer;
  0;;  

/*******************************************************************************
 cbActivate() - Standard callback function for plugin actions
 o      -> Ob       : Ob to which the action is associated
 from   -> DMI      : Not used
 action -> S        : Action
 param  -> S        : Parameter
 reply  -> S        : Not used
 l      -> Anchor   : List of anchors used by the plugin
        <- I        : Not used
*******************************************************************************/
fun activate(o,from,action,param,reply,l)=

   if !strcmp action (strcatn (ObName o)::".gpsupdate"::nil) then 
     ( 
	  //if (timerOn == "top") then
	   /* if !strcmp action "top" then
	    (
		 _DMSevent this "top" nil nil;   		
		 _echo strcatn "Z:"::"z"::nil
		0;)
	  else
	    (_echo strcatn "N:"::"n"::nil 
		0;);*/

	  UpdateGeo param o;
	  echo strcatn "Update:"::(ObName o)::nil
	  0;
	 ) 
   else 
   if !strcmp action (strcatn (ObName o)::".gpsread"::nil) then
     ( 
	    SetCamDefault;	
		echo strcatn "Read:"::(ObName o)::nil
		0;
     )   
   else 
   if !strcmp action (strcatn (ObName o)::".gpsinit"::nil) then
    ( 
		 SetCamOnTop 1;
		 //Init param o; /*enable it for gps err correction */
		 echo "Init..."
		 0;
   )
   else 
   if !strcmp action (strcatn (ObName o)::".gpstopview"::nil) then
    ( 
		 SetCamOnTop 1;
		 echo "Cam Pos..."
		 0;
   )   
   else nil;;

/*******************************************************************************
 cbComm() - Standard cbcomm callback function for intra plugin messages
 ui     -> UserI    : User Instance linked to Ob
 action -> S        : Action
 param  -> S        : Parameter
 l      -> Anchor   : List of anchors used by the plugin
        <- I        : Not used
*******************************************************************************/
fun cbComm(ui,action,param,l)=
   if !strcmp action "gpsupdate" then 
     (apply_on_list l @UpdateGeo param; 0) 
   else 
   if !strcmp action "gpsread" then
     (SetCamDefault; 0)  
   else 
   if !strcmp action "gpsinit" then
     (SetCamOnTop 1; 0)  
   else 
   if !strcmp action "gpstopview" then
     (SetCamOnTop 1; 0)  	 
   else nil;;

fun newOb(o)=
   set sno = rand;
   set nsno = strcatn (ObName o)::"/"::(itoa sno)::nil;
   
   let strcatn "session_"::(itoa sno)::".txt"::nil -> ses in
   let strcat sessionpath ses -> sesfile in
   _storepack "init session\n" sesfile;

  //instance ui values
  let atoi hd UgetParam ObUi o "initav" -> av in
  let atoi hd UgetParam ObUi o "initor" -> or in
  let atoi hd UgetParam ObUi o "initli" -> li in
  let atoi hd UgetParam ObUi o "initca" -> ca in
  
  InitializeScene av or li ca ;
  
  ObRegisterAction o (strcatn (ObName o)::".gpsupdate"::nil) mkfun6 @activate nil;
  ObRegisterAction o (strcatn (ObName o)::".gpsread"::nil) mkfun6 @activate nil;
  ObRegisterAction o (strcatn (ObName o)::".gpsinit"::nil) mkfun6 @activate nil;    
  ObRegisterAction o (strcatn (ObName o)::".gpstopview"::nil) mkfun6 @activate nil;
0;; 

fun destroyPlug()=  

  deleteTimer;

  /*can't in cache...
  let strcatn (sessionpath)::"gpsroute.txt"::nil -> routeBuf in
  _deletepack routeBuf;
  */
 0;;   

fun IniPlug(file)=
 set class=getInfo strextr _getpack _checkpack file "name";
 set timer = _rfltimer _starttimer _channel calibrationtime @rflTimer 0 ;   
 PlugRegister class @newOb nil;  
 0;;